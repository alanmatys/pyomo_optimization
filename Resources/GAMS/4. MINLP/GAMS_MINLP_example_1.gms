Set      t       /1*3/;

Variable b(t)       'Status of the operation of oil-based generation unit in each period';
Binary Variable b;
Variable V(t)       'Volume of oil in storage tank in each period';
Positive Variable V;
V.fx(t)$(ord(t)=card(t)) = 2000;
V.up(t) = 4000;
Variable x(t)       'Oil consumption in each period';
Positive Variable x;
Variable y(t)       'Power generated by other units in each period';
Positive Variable y;
y.lo(t) = 50;
y.up(t) = 700;
Variable h(t)       'Power generated by oil-based generation unit in each period';
Positive Variable h;

Variable z          'Objective function value';

Parameter lam(t)    'System load in each period'
/1       400
 2       900
 3       700/;

Parameter mu        'Initial level of the oil-storage tank in the first time period'
/3000/;

Equation  obj       'Objective function';
          obj..     z =e= sum(t,300 + 6*y(t) + 0.0025*(y(t)**2));

Equation  eq1(t)    'Minimum level of power generation by oil-based generation unit in each period';
          eq1(t)..  h(t) =g= 100*b(t);

Equation  eq2(t)    'Maximum level of power generation by oil-based generation unit in each period';
          eq2(t)..  h(t) =l= 500*b(t);

Equation  eq3a(t)    'Volume balance for oil storage tank in period t = 1';
          eq3a(t)$(ord(t)=1)..  V(t) =e= 500 - x(t) + mu;

Equation  eq3b(t)    'Volume balance for oil storage tank in periods t > 1';
          eq3b(t)$(ord(t)>1)..  V(t) =e= V(t-1) + 500 - x(t);

Equation  eq4(t)    'Calculation of oil consumption in each period';
          eq4(t)..  x(t) =e= 50*b(t) + h(t) + 0.005*(h(t)**2);

Equation  eq5(t)    'Total power generated by oil-based, as well as other generation units must meet the demand in each period';
          eq5(t)..  h(t) + y(t) =g= lam(t);

Model GAMS_MINLP_example_1 /all/;

option optCr = 0;

Solve GAMS_MINLP_example_1 using MINLP minimizing z;
